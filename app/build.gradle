apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.promact.testingdeployment"
        minSdkVersion 14
        targetSdkVersion 23
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        development {
            println("\n===================== For development Branch =======================")
            versionCode 2
            versionName "2.0"
            printf("\n--------" + "VERSION DATA--------" + "\n" +
                    "- CODE: " + versionCode + "\n" +
                    "- NAME: " + versionName + "\n" +
                    "----------------------------\n")
        }
        betatesters {
            println("\n\n===================== For beta-Testers Branch =======================")
            versionCode getTagVersionCode()
            versionName getTagVersionName()
            printf("\n--------" + "VERSION DATA--------" + "\n" +
                    "- CODE: " + versionCode + "\n" +
                    "- NAME: " + versionName + "\n" +
                    "----------------------------\n")
        }
    }
}
// Method to make version name
def getTagVersionName() {


    // Fetch the version according to git latest tag and "how far are we from last tag"
    def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
    def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
    def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
    return "$versionMajor.$versionMinor.$versionPatch($gitSha)"
}

// Method to make version code
def getTagVersionCode() {
    def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
    def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
    def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
    return versionMajor.toInteger() * 1000 +
            versionMinor.toInteger() * 100 +
            versionPatch.toInteger()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
}
